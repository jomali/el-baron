

!!==============================================================================
!!
!!	EL BARÓN
!!	Escena primera
!!
!!==============================================================================
!!
!!	Archivo:			baronSceneOne.xinf
!!	Autor(es):		Victor Gijsbers
!!								J. Francisco Martín <jfm.lisaso@gmail.com>
!!	Idioma:				ES (Español)
!!	Sistema:			Inform-INFSP 6
!!	Plataforma:		Máquina-Z/Glulx
!!	Versión:			0.0
!!	Fecha:				2024/XX/XX
!!
!!------------------------------------------------------------------------------
!!
!!	Este archivo forma parte de la ficción interactiva EL BARÓN.
!!
!!	EL BARÓN es software libre: usted puede redistribuirlo y/o modificarlo bajo 
!!	los términos de la Licencia Pública General GNU publicada por la Fundación 
!!	para el Software Libre, ya sea la versión 3 de la Licencia, o (a su 
!!	elección) cualquier versión posterior.
!!
!!	EL BARÓN se distribuye con la esperanza de que sea útil, pero SIN GARANTÍA 
!!	ALGUNA; ni siquiera la garantía implícita MERCANTIL o de APTITUD PARA UN 
!!	PROPÓSITO DETERMINADO. Consulte los detalles de la Licencia Pública General 
!!	GNU para más información.
!!
!!	Debería haber recibido una copia de la Licencia Pública General GNU junto a 
!!	EL BARÓN. En caso contrario, consulte <http://www.gnu.org/licenses/>.
!!
!!	Copyright (c) 2004, Victor Gijsbers
!!	Copyright (c) 2024, J. Francisco Martín
!!
!!------------------------------------------------------------------------------

[ FinalEscena1;
	Utils.erase_window();
	new_line;
	print (strong) Story, " — Primera escena^";
	new_line;
	print "Una historia interactiva de Victor Gijsbers.^";
	print "Versión en castellano por J. Francisco Martín.^";
	new_line;
	return Utils.press_intro_to_continue(FinalEscena1Citas);
];

[ FinalEscena1Citas;
	Utils.erase_window();
	new_line;
	print (stressed) "Chi l'anima mi lacera?^
	Chi m'agita le viscere?^
	Che strazio, ohimè, che smania!^
	Che inferno, che terror!^";
	new_line;
	print (stressed) "«¿Quién es aquel que lacera mi alma?^
	¿Quién aquel que tortura mi cuerpo?^
	¡Qué tormento! ¡Oh! ¡Qué agonía!^
	¡Qué infierno! ¡Qué terror!»^";
	new_line;
	print "De la ópera ~Don Giovanni~^
	Texto: Lorenzo da Ponte^
	Música: Wolfgang A. Mozart^";
	new_line;
	Utils.end_history();
];

!! @Armadura
Item Armadura Limbo
	class
		Clothes,
	has
		male,
	with
		short_name [;
			print "conjunto completo de [](armadura:REMARKABLE_OBJECT)";
			return true;
		],
		name_f 'armadura' 'cota' 'malla',
		name_fp 'cintas' 'mallas' 'placas',
		name_m 'guantelete',
		name_mp 'guanteletes',
		gender G_MASCULINO,
		adjectives 'acero' 'completo' 'conjunto' 'cuero',
		!!
		before [;
			Disrobe:
				if (~~Baron.is_burning) {
					print "No puedes quitarte la [self](armadura:OTHER_OBJECT) sin ayuda; está asegurada con apretadas cintas de cuero a tu espalda.";
				} else {
					print "El metal fundido de la [self](armadura:OTHER_OBJECT) te perfora el cuerpo. Querrías quitártela. Arrancarte la piel. Pero no tienes cómo.";
				}
				new_line;
				return true;
		],
		description [;
			if (~~Baron.is_burning) {
				if (real_location == GrutaPasadizo or GrutaSalon) {
					print "Por su culpa estás sudando copiosamente, pero jamás";
				} else {
					print "Jamás";
				}
				print " te enfrentarías a la bestia sin un conjunto completo de [self](armadura:OTHER_OBJECT). Gruesas placas de acero sobre cota de mallas —todo ello engrasado concienzudamente—, con lo que te aseguras la mayor protección posible ante cualquier tipo de ataque físico. El conjunto está ajustado por unas cintas de cuero a la espalda.";
			} else {
				print "Incluso el acero de la [self](armadura:OTHER_OBJECT) se está fundiendo con el calor del fuego. Sientas las gotas de metal líquido abriéndose paso a través de tu piel como cuchillos.";
			}
			new_line;
			return true;
		],
;

!! @GrutaBaronFuego
Atrezzo -> GrutaBaronFuego "fuego"
	class
		Fire,
	with
		article "el",
		before [;
			Drop:
				print "¡Si resultara tan sencillo!";
				new_line;
				return true;
			SwitchOff:
				print "Tratas de apagar las llamas desesperado pero resulta inútil; cada vez crecen y arden con mayor ferocidad.";
				new_line;
				return true;
		],
		daemon [;
			!! Es posible interrumpir su ejecución durante un turno asignando el
			!! atributo `on` al objeto:
			if (self has on) {
				give self ~on;
				return true;
			}
			return self.acts();
		],
		description [;
			SetPronoun('-las', self);
			SetPronoun('ellas', self);
			print "Un turbulento mar de llamas te envuelve por completo.";
			new_line;
			return true;
		],
		invent [;
			if (inventory_stage == 1) {
				print "el [self](fuego) contigo";
				return true;
			}
		],
	private
		acts [;
			self.counter++;
			switch (self.counter) {
				3:
					new_line;
					print "El metal de tu [Armadura](armadura) está cada vez más y más caliente a medida que las llamas crecen a tu alrededor.";
					new_line;
					return true;
				4:
					new_line;
					print "El dolor es mayor por momentos. Golpeas las llamas angustiado, intentando apagarlas. Pero no funciona. Peor, te das cuenta con horror que la propia [Armadura](armadura) comienza a fundirse lentamente.";
					new_line;
					return true;
				6:
					!! TODO - traducir
					new_line;
					print "Melting metal penetrates your skin and commingles with your liquifying flesh. No visions, no sounds, no thoughts: only pain fills your consciousness as you fall to your knees, screaming your last scream while your vocal cords burn away.^^
					La oscuridad cae sobre ti como una fría manta.";
					new_line;
					return Utils.press_intro_to_continue(FinalEscena1);
			}
		],
		counter 0,
;

!! @GrutaLuz
BlindingLight -> GrutaLuz "luz"
	has
		female on,
	private
		cant_see [;
			print "Sólo hay blanco. [Una self] de un blanco perfecto ocupándolo todo.";
			new_line;
			return true;
		],
	with
		name_f "claridad" "llama" "luz",
		name_fp "llamas",
		name_m "fuego" "fulgor" "resplandor",
		name_mp "fuegos",
		gender G_FEMENINO,
		adjectives "blanca" "blanco",
		!!
		description [;
			if (self hasnt described) {
				print "Te das cuenta de algo que rompe apenas el vacío blanco. Un pequeño borrón, casi imperceptible, delante tuyo.
				^^";
				if (GrutaVestibuloPuertas has described
					|| GrutaVestibuloPuertasGrabado has described) {
					print "Al acercarte, la débil sombra va creciendo y cogiendo forma ante tus ojos, hasta convertirse en la talla del hombre de la puerta, ardiendo entre sus llamas de bronce. —¡Disfruta del calor!— te grita sin palabras —Aprenderás a amarlo.
					^^
					Quieres responder pero el fuego arrasa tus cuerdas vocales. Tus piernas no pueden sostenerte por más tiempo y te desplomas, fundiéndote en la nada.";
				} else {
					print "Al acercarte, la débil sombra va creciendo y cogiendo forma ante tus ojos, hasta convertirse en la silueta de un hombre. Entonces tus piernas ceden, incapaz de sostenerte por más tiempo y te desplomas en el suelo. Intentas gritar, pedir ayuda al desconocido. Pero el fuego arrasa tus cuerdas vocales.
					^^
					Te fundes en la luz.";
				}
				new_line;
				Utils.press_intro_to_continue(FinalEscena1);
			} else {
				<<Look>>;
			}
			new_line;
			return true;
		],
		react_before [;
			Attack,
			KnockOn:
				return self.cant_see();
			Drop:
				return false;
			TalkTo:
				return self.cant_see();
			default:
				return self.BlindingLight::react_before();
		],
;

!! @LogroBerserker
Object LogroBerserker "Berserker"
	with
		description [;
			print "Te has enfrentado al dragón con el rostro descubierto y completamente desarmado.";
			new_line;
			return true;
		],
;

!! @LogroCodigoCaballeria
Object LogroCodigoCaballeria "Código de caballería"
	with
		description [;
			print "Te has enfrentado al dragón respetando el código de honor de la orden de caballería; equipado con arma y armadura, pero con el rostro descubierto para que la bestia pueda mirarte a los ojos en el momento de acabar con su vida.";
			new_line;
			return true;
		],
;

!! @LogroExcavandoDemasiadoProfundo
Object LogroExcavandoDemasiadoProfundo "Excavando demasiado profundo"
	with
		description [;
			print "Has muerto al caer en un lago de magma en lo más profundo de la gruta.";
			new_line;
			return true;
		],
;

!! @LogroGongInfernal
Object LogroGongInfernal "Un gong infernal"
	with
		description [;
			print "Has aporreado las puertas de bronce intentando huir del dragón pero nadie ha respondido a tu llamada.";
			new_line;
			return true;
		],
;

!! @LogroGuerreroObstinado
Object LogroGuerreroObstinado "Guerrero obstinado"
	with
		description [;
			print "Has cargado contra el dragón incluso a pesar de estar envuelto en llamas, después de que éste lanzase su aliento de fuego sobre ti.";
			new_line;
			return true;
		],
;

!! @LogroHombreArdiente
Object LogroHombreArdiente "El hombre ardiente"
	with
		description [;
			print "Has ardido durante varios turnos después de que el dragón haya escupido un chorro de fuego sobre ti, hasta llegar a ver cómo tu armadura empezaba a fundirse por el calor.";
			new_line;
			return true;
		],
;

!! @LogroMasValePrevenir
Object LogroMasValePrevenir "Más vale prevenir"
	with
		description [;
			print "Te has enfrentado al dragón asumiendo de antemano todas las precauciones posibles: equipado con arma y armadura, y tomando prestado el casco de uno de los héroes que lo intentaron antes que tú.";
			new_line;
			return true;
		],
;

!! @LogroYEseRuido
Object LogroYEseRuido "¿Y ese ruido?"
	with
		description [;
			print "Lo último que has escuchado es un terrible crujido mientras el dragón se te comía vivo.";
			new_line;
			return true;
		],
;

!! @Percunos
Item Percunos Limbo
	has
		male remarkable,
	with
		short_name [;
			print "[](hacha:REMARKABLE_OBJECT) de guerra";
			return true;
		],
		name_m 'arma' 'hacha' 'percunos' 'perkwunos',
		gender G_MASCULINO,
		adjectives 'batalla' 'guerra',
		article "tu",
		!!
		description [;
			print "Para esta gesta te has traído el más pesado de tus hachas de guerra. Pocos pueden levantar su peso y, menos aún, manejarlo con tu misma velocidad y precisión letales.";
			new_line;
			return true;
		],
		invent [;
			if (inventory_stage == 1 && parent(self) ofclass Room) {
				print "tu [self](hacha) a un lado";
				return true;
			}
		],
;

!! @Yelmo
!! TODO - si el barón está ardiendo, llevar el yelmo al limbo
Item Yelmo "yelmo" Limbo
	class
		Clothes,
	has
		male ~open openable transparent,
	with
		name_m 'casco' 'morrion' 'yelmo',
		gender G_MASCULINO,
		adjectives 'antiguo' 'ennegrecido' 'funcional' 'negro' 'viejo',
		!!
		after [;
			Disrobe:
				print "Te l[o self] quitas.";
				new_line;
				return true;
			Take:
				print "Recoges [el self] del suelo.";
				new_line;
				return true;
			Wear:
				if (~~self.tried_to_wear) {
					self.tried_to_wear = true;
					print "L[o self] sacudes enérgicamente antes de ponértel[o self], vaciando una cascada de ceniza sobre el suelo. Los sonidos de la cueva parecen intensificarse en su interior.";
					new_line;
					return true;
				}
		],
		before [;
			Drop:
				if (self in player && self has worn) {
					print "Te l[o self] quitas y lo posas en el suelo.";
					new_line;
					return true;
				}
			Open:
				print "[El YelmoVisor] está atascado. No consigues abrirlo.";
				new_line;
				return true;
			Wear:
				if (TestScope(self, player) && self notin player) {
					move self to player;
					if (~~self.tried_to_wear) {
						self.tried_to_wear = true;
						print "Recoges [el self] y l[o self] sacudes enérgicamente antes de ponértel[o self], vaciando una cascada de ceniza sobre el suelo. Los sonidos de la cueva parecen intensificarse en su interior.";
					} else {
						print "Recoges [el self] del suelo y te l[o self] pones.";
					}
					new_line;
					return true;
				}
		],
		describe [;
			if (self in GrutaVestibulo) {
				return true;
			}
		],
		description [;
			if (~~Baron.is_burning) {
				if (self in player && self has worn) {
					print "[El YelmoVisor] está cerrado, protegiéndote el rostro. Puedes ver";
					switch (real_location) {
						GrutaPasadizo:
							print " el [real_location](corredor)";
						GrutaSalon:
							print " la [real_location](gruta)";
						GrutaVestibulo:
							print " el [real_location](vestíbulo)";
					}
					print " a través de una estrecha abertura.";
				} else {
					print "[Un self] de acero negro, sin adornos de ningún tipo. Tiene [el YelmoVisor] cerrad[o YelmoVisor].";
				}
			} else {
				print "De algún modo antinatural, el metal [del self] permanece frío en mitad del infierno.";
			}
			new_line;
			return true;
		],
		invent [;
			if (inventory_stage == 1 && parent(self) ofclass Room) {
				print "un viejo [self](casco) tirado en el suelo";
				return true;
			}
		],
		react_before [;

		],
	private
		tried_to_wear false,
;

!! @YelmoVisor
Item -> -> YelmoVisor "visor"
	has
		male,
	with
		name_f 'visera',
		name_m 'visor',
		gender G_MASCULINO,
		adjectives 'casco' 'yelmo',
		!!
		react_before [;
			if (noun == self 
				&& action ~= ##DefaultAction
				&& action ~= ##Examine) {
				noun = Yelmo;
			}
		],
		before [;
			DefaultAction,
			Examine:
				<<Open Yelmo>>;
		],
;

Include "scenes/scene1/1-1-abismo.inf";
Include "scenes/scene1/1-2-vestibulo.inf";
Include "scenes/scene1/1-3-pasadizo.inf";
Include "scenes/scene1/1-4-guarida.inf";
