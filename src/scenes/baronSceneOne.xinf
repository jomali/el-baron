

!!==============================================================================
!!
!!	EL BARÓN
!!	Escena primera
!!
!!==============================================================================
!!
!!	Archivo:		baronSceneOne.xinf
!!	Autor(es):		Victor Gijsbers
!!					J. Francisco Martín <jfm.lisaso@gmail.com>
!!	Idioma:			ES (Español)
!!	Sistema:		Inform-INFSP 6
!!	Plataforma:		Máquina-Z/Glulx
!!	Versión:		0.0
!!	Fecha:			2023/XX/XX
!!
!!------------------------------------------------------------------------------
!!
!!	Este archivo forma parte de la ficción interactiva EL BARÓN.
!!
!!	EL BARÓN es software libre: usted puede redistribuirlo y/o
!!	modificarlo bajo los términos de la Licencia Pública General
!!	GNU publicada por la Fundación para el Software Libre, ya
!!	sea la versión 3 de la Licencia, o (a su elección) cualquier
!!	versión posterior.
!!
!!	EL BARÓN se distribuye con la esperanza de que sea útil,
!!	pero SIN GARANTÍA ALGUNA; ni siquiera la garantía implícita
!!	MERCANTIL o de APTITUD PARA UN PROPÓSITO DETERMINADO.
!!	Consulte los detalles de la Licencia Pública General GNU
!!	para más información.
!!
!!	Debería haber recibido una copia de la Licencia Pública
!!	General GNU junto a EL BARÓN. En caso contrario, consulte
!!	<http://www.gnu.org/licenses/>.
!!
!!	Copyright (c) 2004, Victor Gijsbers
!!	Copyright (c) 2023, J. Francisco Martín
!!
!!------------------------------------------------------------------------------


!!==============================================================================
!!
!!	0)
!!
!!------------------------------------------------------------------------------

!! @Armadura
Item Armadura Limbo
	class
		Clothes,
	has
		male,
	with
		short_name [;
			print "conjunto completo de [](armadura:REMARKABLE_OBJECT)";
			return true;
		],
		name_f 'armadura' 'cota' 'malla',
		name_fp 'cintas' 'mallas' 'placas',
		name_m 'guantelete',
		name_mp 'guanteletes',
		gender G_MASCULINO,
		adjectives 'acero' 'completo' 'conjunto' 'cuero',
		!!
		before [;
			Disrobe:
				if (~~Baron.is_burning) {
					print "No puedes quitarte la [self](armadura:OTHER_OBJECT) sin ayuda; está asegurada con apretadas cintas de cuero a tu espalda.";
				} else {
					print "El metal fundido de la [self](armadura:OTHER_OBJECT) te perfora el cuerpo. Querrías quitártela. Arrancarte la piel. Pero no tienes cómo.";
				}
				new_line;
				return true;
		],
		description [;
			if (~~Baron.is_burning) {
				if (real_location == GrutaPasadizo or GrutaSalon) {
					print "Por su culpa estás sudando copiosamente, pero jamás";
				} else {
					print "Jamás";
				}
				print " te enfrentarías a la bestia sin un conjunto completo de [self](armadura:OTHER_OBJECT). Gruesas placas de acero sobre cota de mallas —todo ello engrasado concienzudamente—, con lo que te aseguras la mayor protección posible ante cualquier tipo de ataque físico. El conjunto está ajustado por unas cintas de cuero a la espalda.";
			} else {
				print "Incluso el acero de la [self](armadura:OTHER_OBJECT) se está fundiendo con el calor del fuego. Sientas las gotas de metal líquido abriéndose paso a través de tu piel como cuchillos.";
			}
			new_line;
			return true;
		],
;

!! @GrutaBaronFuego
Atrezzo -> GrutaBaronFuego "fuego"
	class
		Fire,
	with
		article "el",
		before [;
			Drop:
				print "¡Si resultara tan sencillo!";
				new_line;
				return true;
			SwitchOff:
				print "Tratas de golpear las llamas desesperado pero resulta inútil; cada vez crecen y arden con mayor ferocidad.";
				new_line;
				return true;
		],
		description [;
			SetPronoun('-las', self);
			SetPronoun('ellas', self);
			print "Estás envuelto en un turbulento mar de llamas.";
			new_line;
			return true;
		],
		invent [;
			if (inventory_stage == 1) {
				print "el [self](fuego) contigo";
				return true;
			}
		],
		reset [;
			move self to Limbo;
		],
;

!! @LogroBerserker
Object LogroBerserker "Berserker"
	with
		description [;
			print "Te has enfrentado al dragón con el rostro descubierto y completamente desarmado.";
			new_line;
			return true;
		],
;

!! @LogroCodigoCaballeria
Object LogroCodigoCaballeria "Código de caballería"
	with
		description [;
			print "Te has enfrentado al dragón respetando el código de honor de la orden de caballería; equipado con arma y armadura, pero con el rostro descubierto para que la bestia pueda mirarte a los ojos en el momento de acabar con su vida.";
			new_line;
			return true;
		],
;

!! @LogroExcavandoDemasiadoProfundo
Object LogroExcavandoDemasiadoProfundo "Excavando demasiado profundo"
	with
		description [;
			print "Has muerto al caer en un lago de magma en lo más profundo de la gruta.";
			new_line;
			return true;
		],
;

!! @LogroGongInfernal
Object LogroGongInfernal "Un gong infernal"
	with
		description [;
			print "Has aporreado las puertas de bronce intentando huir del dragón pero nadie ha respondido a tu llamada.";
			new_line;
			return true;
		],
;

!! @LogroGuerreroObstinado
Object LogroGuerreroObstinado "Guerrero obstinado"
	with
		description [;
			print "Has cargado contra el dragón incluso a pesar de estar envuelto en llamas, después de que éste lanzase su aliento de fuego sobre ti.";
			new_line;
			return true;
		],
;

!! @LogroHombreArdiente
Object LogroHombreArdiente "El hombre ardiente"
	with
		description [;
			print "Has ardido durante varios turnos después de que el dragón haya escupido un chorro de fuego sobre ti, hasta llegar a ver cómo tu armadura empezaba a fundirse por el calor.";
			new_line;
			return true;
		],
;

!! @LogroMasValePrevenir
Object LogroMasValePrevenir "Más vale prevenir"
	with
		description [;
			print "Te has enfrentado al dragón asumiendo de antemano todas las precauciones posibles: equipado con arma y armadura, y tomando prestado el casco de uno de los héroes que lo intentaron antes que tú.";
			new_line;
			return true;
		],
;

!! @LogroYEseRuido
Object LogroYEseRuido "¿Y ese ruido?"
	with
		description [;
			print "Lo último que has escuchado es un terrible crujido mientras el dragón se te comía vivo.";
			new_line;
			return true;
		],
;

!! @Percunos
Item Percunos Limbo
	has
		male remarkable,
	with
		short_name [;
			print "[](hacha:REMARKABLE_OBJECT) de guerra";
			return true;
		],
		name_m 'arma' 'hacha' 'percunos' 'perkwunos',
		gender G_MASCULINO,
		adjectives 'batalla' 'guerra',
		article "tu",
		!!
		description [;
			print "Para esta gesta te has traído el más pesado de tus hachas de guerra. Pocos pueden levantar su peso, y menos aún manejarlo con la misma velocidad y precisión mortíferas con las que tú lo haces.";
			new_line;
			return true;
		],
		invent [;
			if (inventory_stage == 1 && parent(self) ofclass Room) {
				print "tu [self](hacha) a un lado";
				return true;
			}
		],
;

!! @Yelmo
!! TODO - si el barón está ardiendo, llevar el yelmo al limbo
Item Yelmo "yelmo" Limbo
	class
		Clothes,
	has
		male ~open openable transparent,
	with
		name_m 'casco' 'morrion' 'yelmo',
		gender G_MASCULINO,
		adjectives 'antiguo' 'ennegrecido' 'funcional' 'negro' 'viejo',
		!!
		after [;
			Disrobe:
				print "Te l[o self] quitas.";
				new_line;
				return true;
			Take:
				print "Recoges [el self] del suelo.";
				new_line;
				return true;
			Wear:
				if (~~self.tried_to_wear) {
					self.tried_to_wear = true;
					print "L[o self] sacudes enérgicamente antes de ponértel[o self], vaciando una cascada de ceniza sobre el suelo. Los sonidos de la cueva parecen intensificarse en su interior.";
					new_line;
					return true;
				}
		],
		before [;
			Drop:
				if (self in player && self has worn) {
					print "Te l[o self] quitas y lo posas en el suelo.";
					new_line;
					return true;
				}
			Open:
				print "[El YelmoVisor] está atascado. No consigues abrirlo.";
				new_line;
				return true;
			Wear:
				if (TestScope(self, player) && self notin player) {
					move self to player;
					if (~~self.tried_to_wear) {
						self.tried_to_wear = true;
						print "Recoges [el self] y l[o self] sacudes enérgicamente antes de ponértel[o self], vaciando una cascada de ceniza sobre el suelo. Los sonidos de la cueva parecen intensificarse en su interior.";
					} else {
						print "Recoges [el self] del suelo y te l[o self] pones.";
					}
					new_line;
					return true;
				}
		],
		describe [;
			if (self in GrutaVestibulo) {
				return true;
			}
		],
		description [;
			if (~~Baron.is_burning) {
				if (self in player && self has worn) {
					print "[El YelmoVisor] está cerrado, protegiéndote el rostro. Puedes ver";
					switch (real_location) {
						GrutaPasadizo:
							print " el [real_location](corredor)";
						GrutaSalon:
							print " la [real_location](gruta)";
						GrutaVestibulo:
							print " el [real_location](vestíbulo)";
					}
					print " a través de una estrecha abertura.";
				} else {
					print "[Un self] de acero negro, sin adornos de ningún tipo. Tiene [el YelmoVisor] cerrad[o YelmoVisor].";
				}
			} else {
				print "De algún modo antinatural, el metal [del self] permanece frío en mitad del infierno.";
			}
			new_line;
			return true;
		],
		invent [;
			if (inventory_stage == 1 && parent(self) ofclass Room) {
				print "un viejo [self](casco) tirado en el suelo";
				return true;
			}
		],
		react_before [;

		],
	private
		tried_to_wear false,
;

!! @YelmoVisor
Item -> -> YelmoVisor "visor"
	has
		male,
	with
		name_f 'visera',
		name_m 'visor',
		gender G_MASCULINO,
		adjectives 'casco' 'yelmo',
		!!
		react_before [;
			if (noun == self 
				&& action ~= ##DefaultAction
				&& action ~= ##Examine) {
				noun = Yelmo;
			}
		],
		before [;
			DefaultAction,
			Examine:
				<<Open Yelmo>>;
		],
;


!!==============================================================================
!!
!!	1)	En el abismo
!!
!!------------------------------------------------------------------------------

!! @Abismo
Indoors Abismo
	has
		concealed male,
	with
		short_name [;
			print "En el abismo";
			return true;
		],
		gender G_MASCULINO,
		!!
		before [;
			Smell:
				print "La [AbismoOscuridad] penetra en tus pulmones, arrastrando consigo una ligera pestilencia.";
				new_line;
				return true;
		],
		description [;
			Tutorial.hint_highlight_key_a();
			if (AbismoOscuridad hasnt described) {
				print "Todo es [AbismoOscuridad] a tu alrededor. El universo entero reducido a un vacío negro.";
			} else {
				print "Distingues un débil [AbismoResplandor](fulgor) entre las sombras.";
			}
			new_line;
			return true;
		],
		each_turn [;
			if (self.counter < 2) {
				self.counter++;
			} else {
				Tutorial.hint_highlight_key_b();
				return true;				
			}
		],
		exits [;
			if (AbismoOscuridad hasnt described) {
				print "No hay ningún lugar al que ir. Nada. Sólo [AbismoOscuridad].";
			} else {
				print "Todo cuanto puedes distinguir en la oscuridad es ese [AbismoResplandor] indefinido que llega desde algún lugar más [f_obj].";
			}
			new_line;
			return true;
		],
		f_to [;
			<<Enter DirAbismoAdelante>>;
		],
		reset [;
			self.counter = 0;
		],
	private
		counter 0,
;

!! @DirAbismoAdelante
AdjacentRoom -> DirAbismoAdelante
	has
		male,
	with
		adjacent_room GrutaVestibulo,
		before [;
			Enter:
				self.AdjacentRoom::onChangeLocation();
				if (AbismoOscuridad hasnt described) {
					print "Alcanzas a distinguir un resplandor entre las sombras.";
				} else {
					print "Ha llegado el momento de enfrentarse a la bestia.";
				}
				print " Tan pronto das un paso en la dirección de la que proviene la luz, suena un portazo atronador a tu espalda seguido del chirrido de cerrojos contra el metal. Ya no hay marcha atrás. Aferras el [Percunos](hacha:REMARKABLE_OBJECT) con firmeza y das otro paso adelante, hacia el calor…";
				new_line;
				!! TODO - Pulsar INTRO para continuar
				self.AdjacentRoom::before();
				return true;
		],
		!! No necesita descripción. Al intentar examinarlo entran en juego las
		!! reglas "react_before" de AbismoOscuridad.
;

!! @AbismoOscuridad
Darkness -> AbismoOscuridad "oscuridad"
	has
		female on,
	with
		name_f 'abismo' 'negrura' 'oscuridad' 'penumbra' 'sombra' 'tiniebla',
		name_fp 'penumbras' 'sombras' 'tinieblas',
		name_m 'vacio',
		gender G_FEMENINO,
		adjectives 'negro',
		article "la",
		!!
		cant_see [;
			if (self hasnt described) {
				print "No puedes ver nada en [la self].";
			} else {
				print "No puedes ver nada en [la self], aparte de [un AbismoResplandor] muy débil que llega desde algún lugar más [f_obj].";
			}
			new_line;
			return true;
		],
		description [;
			if (self hasnt described) {
				move AbismoResplandor to Abismo;
				print "Te dejas llevar por el abrazo de las [self](sombras). Solo e inmóvil en mitad de la nada, lejos de obligaciones, de las monótonas rutinas del día a día, de la gente y sus juicios constantes. Casi desearías dejarlo todo y quedarte aquí para siempre. Pero no es más que una idea infantil, por supuesto.
				^^
				Alcanzas a percibir [un AbismoResplandor] rompiendo apenas la oscuridad.";
			} else {
				<<Look>>;
			}
			new_line;
			return true;
		],
		interactive_objects AbismoResplandor,
;

!! @AbismoResplandor
Background AbismoResplandor "resplandor" Limbo
	has
		male,
	with
		name_f 'luminosidad' 'luz',
		name_m 'fulgor' 'resplandor',
		gender G_MASCULINO,
		adjectives
			'anaranjada' 'anaranjado' 'imprecisa' 'impreciso' 'tenue',
		!!
		description [;
			print "Se trata de una luz parpadeante muy tenue. Llega desde algún lugar más [f_obj].";
			new_line;
			return true;
		],
		reset [;
			move self to Limbo;
		],
;


!!==============================================================================
!!
!!	2)	Vestíbulo de la gruta
!!
!!------------------------------------------------------------------------------

!! @DirGrutaVestibulo
AdjacentRoom DirGrutaVestibulo "vestíbulo"
	has
		male,
	with
		found_in GrutaPasadizo,
		name_f 'antesala',
		name_m 'hall' 'recibidor' 'vestibulo',
		gender G_MASCULINO,
		adjectives 'fresca' 'fresco' 'pequena' 'pequeno' 'rectangular',
		!!
		adjacent_room GrutaVestibulo,
		before [;
			Enter:
				self.AdjacentRoom::before();
				return true;
		],
		description [;
			if (~~Baron.is_burning) {
				print "El relativo frescor [del self] aguarda un poco más allá.";
			} else {
				print "¡La salida! Sólo un poco más allá.";
			}
			new_line;
			return true;
		],
;

!! @GrutaVestibulo
Indoors GrutaVestibulo
	has
		male,
	with
		short_name [;
			print "[](Vestíbulo) de la gruta";
			return true;
		],
		name_f 'antesala' 'caverna' 'cueva' 'estancia' 'gruta' 'sala',
		name_m 'hall' 'recibidor' 'vestibulo',
		gender G_MASCULINO,
		adjectives 'fresca' 'fresco' 'pequena' 'pequeno' 'rectangular',
		!!
		b_to [;
			<<Open GrutaVestibuloPuertas>>;
		],
		before [;
			Dig:
				<<Search GrutaVestibuloHuesos>>;
			Listen:
				if (Baron.is_burning) {
					print "El fuego ruge en tus oídos como un animal hambriento.";
				} else if (noun == 0 or self) {
					print "Una especie de reverberación grave y discontinua parece propagarse por las paredes.";
				} else if (TestScope(noun, player) && ~~noun.before()) {
					print "Prestas atención [al noun] pero no escuchas nada extraño. El único sonido que llega hasta tus oídos es [if:self.tried_to_listen == true]esa[else]una[fi] especie de gruñido grave que parece surgir de las profundidades de la gruta.";
				}
				self.tried_to_listen = true;
				new_line;
				return true;
			Smell:
				if (noun == 0 or self) {
					print "Un penetrante olor como el de los huevos podridos llena la [self](cueva).";
				} else {
					print "Un hedor como a huevos podridos lo impregna todo.";
				}
				new_line;
				return true;
		],
		description [;
			Tutorial.hint_keywords_a();
			print "Esta pequeña sala rectangular ha sido excavada en la roca y sus paredes y su techo han sido recubiertos después con finas planchas de [GrutaVestibuloMarmol] negro. Dos [GrutaVestibuloPuertas] sellan la única salida. Frente a ellas, en la pared opuesta del vestíbulo, un estrecho [DirGrutaPasadizo](corredor) se interna aún más en las ardientes profundidades de la montaña.";
			new_line;
			return true;
		],
		exits [;
			if (~~Baron.is_burning) {
				print "Aparte de la salida bloqueada por las puertas únicamente se puede seguir [f_obj]. Hacia el fuego.";
			} else {
				print "Las [GrutaVestibuloPuertas](puertas:DIRECTION_OBJECT) son la única salida de este infierno.";
			}
			new_line;
			return true;
		],
		f_to [;
			<<Enter DirGrutaPasadizo>>;
		],
		in_to [;
			<<Enter DirGrutaPasadizo>>;
		],
		out_to [;
			<<Enter GrutaVestibuloPuertas>>;
		],
		reset [;
			self.tried_to_listen = false;
		],
	private
		tried_to_listen false,
;

!! @GrutaVestibuloPuertas
Item -> GrutaVestibuloPuertas
	has
		door female openable ~open pluralname scenery transparent,
	with
		short_name [;
			print "[](puertas:REMARKABLE_OBJECT) de bronce";
			return true;
		],
		name_f 'entrada' 'puerta' 'salida',
		name_fp 'puertas',
		gender G_FEMENINO + G_PLURAL,
		adjectives 'bronce' 'cerrada' 'cerradas',
		!!
		before [;
			Attack,
			KnockOn:
				if (~~Baron.is_burning) {
					print "¿Sin haberte enfrentado antes a la bestia? No. No eres un cobarde.";
				} else {
					!! TODO - Llamada después de haber visto al dragón
				}
				new_line;
				return true;
			Lock,
			Unlock:
				print "En este lado no hay ningún cerrojo.";
				new_line;
				return true;
			Open,
			Pull,
			Push,
			PushDir:
				if (~~Baron.is_burning) {
					print "Las [self](puertas) han sido trancadas desde el otro lado y ni siquiera el monstruo podría abrirlas desde aquí. El acuerdo es que llamarás con un golpe para que te abran una vez hayas lo hayas derrotado.";
				} else {
					<<KnockOn self>>;
				}
				new_line;
				return true;
			Touch:
				if (~~Baron.is_burning) {
					print "Los guanteletes te impiden tocar el bronce directamente, pero aún así puedes notar perfectamente el calor que irradia.";
				} else {
					print "Con los sentidos completamente embotados por el dolor no eres capaz de notar nada.";
				}
				new_line;
				return true;
		],
		description [;
			if (~~Baron.is_burning) {
				print "Dos imponentes puertas de bronce bloquean la única salida de la gruta.";
				if (self hasnt described) {
					print " Tienen unos grabados ornamentales, de factura muy tosca, entre los que adviertes la figura de un [GrutaVestibuloPuertasGrabado](hombre:REMARKABLE_OBJECT) que parece arder en llamas.";
				} else {
					print " Están decoradas con el [GrutaVestibuloPuertasGrabado](grabado) de un hombre que arde en llamas.";
				}
			} else {
				!! TODO Barón en llamas
			}
			new_line;
			return true;
		],
;

!! @GrutaVestibuloPuertasGrabado
Atrezzo -> -> GrutaVestibuloPuertasGrabado
	has
		male,
	with
		short_name [;
			print "[](hombre) en llamas";
			return true;
		],
		name_f 'figura' 'persona' 'talla',
		name_m 'adorno' 'caballero' 'grabado' 'hombre',
		gender G_MASCULINO,
		adjectives 'ardiendo' 'llamas' 'un' 'una',
		!!
		before [
			obj;
			DefaultAction:
				if (self hasnt described || self.tried_to_speak) {
					<<Examine self>>;
				} else {
					<<TalkTo self>>;
				}
			Pull,
			Push,
			PushDir,
			Turn:
				obj = parent(self);
				<<Push obj>>;
			Take:
				obj = parent(self);
				<<Take obj>>;
			TalkTo:
				!! TODO hablar con hombre mientras estás ardiendo
				if (ConversacionHombreArdiendo.has_ended() 
					&& ~~self.tried_to_speak) {
					self.tried_to_speak = true;
					print "El hombre parece perder interés en ti. —Pronto. Muy pronto —repite, antes de volver toda su atención de nuevo a sus llamas de metal.";
					new_line;
					return true;
				} else if (ConversacionHombreArdiendo.has_ended()) {
					print "La figura del hombre tiene toda su atención puesta en su mundo de metal.";
					new_line;
					return true;
				} else {
					return ChoiceSetManager.start(ConversacionHombreArdiendo);
				}
			Touch:
				obj = parent(self);
				<<Touch obj>>;
		],
		description [;
			if (self hasnt described && ~~Baron.is_burning) {
				ChoiceSetManager.start(ConversacionHombreArdiendo, true);
				print "Su rostro, lejos de reflejar la expresión de tormento que cabría esperar de un hombre ardiendo, muestra algo muy distinto; un ansia profundo, excitación. No agoniza en las llamas de bronce sino que se deleita con ellas.
				^^
				Al observarla bajo la luz cambiante de la gruta, la [self](figura) parece contorsionarse lentamente, danzando junto a las sombras. Como si estuviera viva. El efecto es tan sugestivo que por un momento tu mente te traiciona y crees escuchar una vocecilla metálica.
				^^
				—¡Un compañero nuevo! ¡Vaya sorpresa! —sus inmóviles ojos recorren los [GrutaVestibuloHuesos](esqueletos) esparcidos por el suelo—. Vas a tener que buscar muy bien para encontrarte un hueco.";
			} else {
				print "Su rostro, lejos de reflejar la expresión de tormento que cabría esperar de un [self](hombre) ardiendo, muestra algo muy distinto; un ansia profundo.";
				if (~~Baron.is_burning) {
					print  "^^
					La voz que has creído escuchar hace un momento ahora guarda silencio.";
				} else {
					print " Horrorizado comprendes que tu propio rostro debe estar mostrando exactamente lo mismo en este momento.";
				}
			}
			new_line;
			return true;
		],
	private
		tried_to_speak false,
;

!! @GrutaVestibuloMarmol
Atrezzo -> GrutaVestibuloMarmol "mármol"
	has
		female pluralname transparent,
	with
		name_f 'pared',
		name_fp 'paredes',
		name_m 'marmol',
		gender G_MASCULINO,
		adjectives 
			'negra' 'negras' 'negro' 'planchas' 'pulida' 'pulidas' 'pulido',
		!!
		before [;
			Touch:
				if (~~Baron.is_burning) {
					print "Las [GrutaVestibuloMarmolVetas] blancas del mármol parecen contorsionarse bajo tu mano, como si estuvieran vivas.";
				} else {
					print "Las [GrutaVestibuloMarmolVetas] blancas del mármol huyen de tu contacto.";
				}
				new_line;
				return true;
		],
		description [;
			print "Tanto las paredes como el techo están cubiertos por unas placas de mármol negro muy pulido. Sus [GrutaVestibuloMarmolVetas] forman patrones intrincados sobre la superficie.";
			new_line;
			return true;
		],
;

!! @GrutaVestibuloMarmolVetas
Atrezzo -> -> GrutaVestibuloMarmolVetas "vetas"
	has
		male pluralname,
	with
		name_f 'estria' 'linea' 'raya' 'veta',
		name_fp 'estrias' 'lineas' 'rayas' 'vetas',
		name_m 'patron',
		name_mp 'patrones',
		gender G_FEMENINO + G_PLURAL,
		adjectives 'blanca' 'blancas' 'blanco' 'blancos' 'marmol',
		!!
		before [
			obj;
			Pull,
			Push,
			PushDir,
			Turn:
				obj = parent(self);
				<<Push obj>>;
			Take:
				obj = parent(self);
				<<Take obj>>;
			Touch:
				obj = parent(self);
				<<Touch obj>>;
		],
		description [;
			if (self hasnt described && ~~Baron.is_burning) {
				print "Al estudiar las líneas del mármol se apodera de ti el presentimiento de que, de algún modo, esas estrías blancas codifican un significado oculto. Como si bajo el aparente caos se hayase la respuesta a un profundo misterio. Pero al esforzarte por desentrañarlo los patrones comienzan a bailar ante tus ojos; se retuercen y giran y se arremolinan unos con otros.
				^^
				Mareado, tienes que apartar la mirada.";
			} else if (~~Baron.is_burning) {
				print "Las ondulantes líneas blancas parecen retorcerse en la superficie del mármol. No eres capaz de fijar la mirada sobre ellas sin marearte.";
			} else {
				print "Las ondulantes líneas blancas se retuercen en la superficie del mármol. Como culebras quemándose vivas.";
			}
			new_line;
			return true;
		],
;

!! @GrutaVestibuloFuego
Atrezzo -> GrutaVestibuloFuego "fuego"
	class
		Background,
	has
		light male,
	with
		name_f 'claridad' 'llama' 'luz',
		name_fp 'llamas',
		name_m 'fuego' 'fulgor' 'resplandor',
		name_mp 'fuegos',
		gender G_MASCULINO,
		adjectives 'lengua' 'lenguas',
		!!
		before [;
			Blow,
			SwitchOff:
				print "Nadie podría apagar un fuego como ese.";
				new_line;
				return true;
		],
		description [;
			print "Un incendio salvaje arde en las profundidades de la gruta y arroja destellos de una luz rojiza y anaranjada a tu alrededor.";
			new_line;
			return true;
		],
;

!! @GrutaVestibuloSombras
Atrezzo -> GrutaVestibuloSombras "sombras"
	class
		Background,
	has
		female pluralname,
	with
		name_f 'oscuridad' 'sombra' 'tiniebla',
		name_fp 'sombras' 'tinieblas',
		gender G_FEMENINO + G_PLURAL,
		!!
		description [;
			if (~~Baron.is_burning) {
				print "Las sombras dibujan formas macabras y temblorosas a tu alrededor.";
			} else {
				print "Las sombras huyen a tu paso.";
			}
			new_line;
			return true;
		],
;

!! @GrutaVestibuloHuesos
Atrezzo -> GrutaVestibuloHuesos "huesos"
	has
		~concealed male pluralname ~scenery static,
	with
		name_f 'calavera',
		name_fp 'calaveras',
		name_m 'craneo' 'esqueleto' 'hueso' 'osario',
		name_mp 'craneos' 'esqueletos' 'huesos',
		gender G_MASCULINO + G_PLURAL,
		adjectives
			'despedazado' 'despedazados' 'ennegrecida' 'ennegrecidas'
			'ennegrecido' 'ennegrecidos' 'humana' 'humanas' 'humano' 'humanos'
			'negra' 'negras' 'negro' 'negros',
		!!
		before [;
			Dig,
			Search:
				if (~~self.tried_to_search) {
					self.tried_to_search = true;
					move Yelmo to parent(self);
					print "La capa de [self](huesos) es mayor de lo que habías imaginado. Hay todo un brazo de profundidad antes de alcanzar el suelo de mármol. ¿Cuántos han venido aquí antes que tú?
					^^
					Al remover los restos descubres algo que estaba enterrado. Un viejo [Yelmo](casco) de acero negro entre las calaveras blancas.";
				} else {
					print "Bajo el manto óseo hay un suelo del mismo [GrutaVestibuloMarmol] negro que recubre el resto de la estancia.";
				}
				new_line;
				return true;
			Take:
				print "Son los restos de aquellos que vinieron aquí antes que tú, y no tienes ninguna intención de cargar con recuerdos de su fracaso.";
				new_line;
				return true;
		],
		describe [
			rnd;
			rnd = random(2);
			new_line;
			if (parent(self) == parent(Yelmo)) {
				print "Un antiguo [Yelmo] descansa entre [los self] del suelo.";
			} else if (real_location hasnt visited || rnd == 1) {
				print "Una auténtica alfombra de [self] oculta el suelo.";
			} else {
				print "[Los self] del suelo proyectan sombras temblorosas a tu alrededor.";
			}
			new_line;
			return true;
		],
		description [;
			print "El suelo está cubierto de esqueletos despedazados. Calcinados por el fuego que en algún momento consumió la grasa y el músculo de sus cuerpos.";
			if (self hasnt described) {
				print "^^
				Arrojan sombras grotescas en la luz titilante.";
			}
			new_line;
			return true;
		],
		reset [;
			self.tried_to_search = false;
		],
	private
		tried_to_search false,
;

!! @ConversacionHombreArdiendo
ChoiceSet ConversacionHombreArdiendo "(Conversación: hombre ardiendo)"
	with
		fixed true,
		final_action [;
			!!
		],
		initial_action [;
			self.add_choice(EstasVivo);
			self.add_choice(EnfrentadoBestia);
		],
		inter_action [;
			print (string) random(
				"La figura de bronce parece seguir tus movimientos con atención.",
				"La figura te observa atentamente."
			);
			new_line;
			return true;
		],
		response [;
			new_line;
			if (self.counter == 0) {
				self.counter++;
				!! TODO si lleva el yelmo puesto
				print "—El rostro descubierto, bien —ignora tu pregunta, pero percibes una nota de aprobación en la vocecilla estridente—. Mucho mejor que la mayoría de esos cobardes.";
			} else {
				!! TODO si lleva el yelmo puesto
				self.counter++;
				self.add_choice(ComoVencer);
				print "—Dime, ¿quién se acerca al fuego si tiene miedo a quemarse las pestañas? —escupe con desprecio un pequeño rodamiento de bronce que se pierde en las sombras.
				^^
				—La bestia —insistes—. ¿Qué aspecto tiene?
				^^
				—¡Oh, lo verás muy pronto!";
			}
			new_line;
			return true;
		],
 private
		counter 0,
;

!! @EstasVivo
ChoiceSetItem EstasVivo
 with	entry [
			previous_style;
			previous_style = Utils.set_text_style(TEXT_STYLE_STRESSED);
			if (ConversacionHombreArdiendo.has_choice(EnfrentadoBestia)) {
				print "—¿Has… has hablado de ";
			} else {
				print "—¿Estás… estás hablado de ";
			}
			PRT__("verdad", 0, REMARKABLE_OBJECT);
			print "? ¿Estás vivo?";
			Utils.set_text_style(previous_style);
			return true;
		],
		keys 'eres' 'verdad',
		!!
		append_choice_set true,
		final_choice false,
		reaction [;
			if (ConversacionHombreArdiendo.has_choice(EnfrentadoBestia)) {
				print "—¿Has… has hablado de ";
			} else {
				print "—¿Estás… estás hablado de ";
			}
			print "verdad? ¿Estás vivo?";
			new_line;
			return ConversacionHombreArdiendo.response();
		],
;

!! @EnfrentadoBestia
ChoiceSetItem EnfrentadoBestia
 with	entry [
			previous_style;
			previous_style = Utils.set_text_style(TEXT_STYLE_STRESSED);
			print "—Eso te lo ha hecho la ";
			PRT__("bestia", 0, REMARKABLE_OBJECT);
			print ", ¿no es así?";
			Utils.set_text_style(previous_style);
			return true;
		],
		keys 'enfrentado' 'bestia' 'dragon' 'monstruo',
		!!
		append_choice_set true,
		final_choice false,
		reaction [;
			print "—Eso te lo ha hecho la bestia, ¿no es así?";
			new_line;
			return ConversacionHombreArdiendo.response();
		],
;

!! @ComoVencer
ChoiceSetItem ComoVencer
 with	entry [;
			print "—¿Sabes cómo puedo ";
			PRT__("vencerla", 0, REMARKABLE_OBJECT);
			print "?";
			return true;
		],
		keys 'como' 'venzo' 'vencer' 'vencerla',
		!!
		append_choice_set false,
		final_choice true,
		reaction [;
			print "—¿Sabes cómo puedo vencerla? Tendrá algún punto débil…
			^^
			El hombre sonríe, señalando de nuevo los huesos a tus pies.
			^^
			—Pregúntales a ellos.";
			new_line;
			return true;
		],
;


!!==============================================================================
!!
!!	3)	Un pequeño pasadizo
!!
!!------------------------------------------------------------------------------

!! @DirGrutaPasadizo
AdjacentRoom DirGrutaPasadizo "pasadizo"
	has
		male,
	with
		found_in GrutaSalon GrutaVestibulo,
		name_m 'corredor' 'pasadizo' 'pasillo',
		gender G_MASCULINO,
		adjectives 'bajo' 'estrecho' 'pequeno',
		!!
		adjacent_room GrutaPasadizo,
		before [;
			Enter:
				switch (location) {
					GrutaSalon:
						GrutaPasadizo.is_leading_inwards = false;
						if (Baron.is_blind) {
							return GrutaSalon.cant_go();
						}
					GrutaVestibulo:
						if (~~Baron.is_burning) {
							print "Los huesos del suelo crujen destrozados bajo tus botas.";
							new_line;
						}
						GrutaPasadizo.is_leading_inwards = true;
				}
				return self.AdjacentRoom::before();
		]
;

!! @GrutaPasadizo
Indoors GrutaPasadizo
	has
		male,
	with
		short_name [;
			print "Un pequeño [](pasadizo)";
			return true;
		],
		name_f 'caverna' 'cueva' 'gruta',
		name_m 'corredor' 'pasadizo' 'pasillo',
		gender G_MASCULINO,
		adjectives 'bajo' 'estrecho' 'pequeno',
		!!
		b_to [;
			<<Enter DirGrutaVestibulo>>;
		],
		before [;

		],
		description [;
			Tutorial.hint_keywords_b();
			if (~~Baron.is_burning) {
				print "[if:self has visited]El [self](corredor) es[else]Entras en un [self](corredor)[fi] largo y estrecho, con[if:self has visited] un[fi] suelo de [Cenizas](ceniza) compactada y un techo tan bajo que te obliga a inclinarte para poder pasar. Desde la [DirGrutaSalon](guarida) del dragón, en el extremo del [self](pasillo), llegan oleadas de un calor infernal que";
				if (self.is_leading_inwards && Yelmo has worn) {
					print " se cuelan entre las rendijas [del Yelmo], abrasándote los ojos,";
				} else if (self.is_leading_inwards) {
					print " te golpean en plena cara";
				} else {
					print " te abrasan la espalda";
				}
				print " y provocan que estés empapado en sudor dentro de tu [Armadura](armadura). [El DirGrutaVestibulo] se antoja ahora fresco en comparación.";
			}
			new_line;
			return true;
		],
		exits [;
			if (self.is_leading_inwards) {
				print "Tienes [el DirGrutaVestibulo] a tu espalda y [el DirGrutaSalon] frente a ti.";
			} else {
				print "Tienes [el DirGrutaVestibulo] frente a ti y [el DirGrutaSalon] a tu espalda.";
			}
			new_line;
			return true;
		],
		f_to [;
			<<Enter DirGrutaSalon>>;
		],
		in_to [;
			<<Enter DirGrutaSalon>>;
		],
		is_leading_inwards true,
		out_to [;
			<<Enter DirGrutaVestibulo>>;
		],
		reset [;
			self.is_leading_inwards = true;
		],
;

!! @Cenizas
Atrezzo -> Cenizas "cenizas"
	has
		female pluralname transparent,
	with
		name_f 'ceniza',
		name_fp 'cenizas',
		gender G_FEMENINO + G_PLURAL,
		adjectives 'capa' 'de' 'del' 'suelo',
		!!
		before [;
			Take:
				!! TODO ardiendo
				if (~~self.tried_to_take) {
					self.tried_to_take = true;
					print "Te agachas para coger un puñado de cenizas del suelo, pero están tan comprimidas que sólo consigues manchar ligeramente el guantelete con el fino polvo.";
				} else {
					print "Están tan comprimidas que resulta imposible.";
				}
				new_line;
				return true;
		],
		description [;
				!! TODO ardiendo
			print "Una gruesa capa de cenizas amortigua en parte tus pisadas. Están compactadas hasta tal punto que tus pies apenas dejan huella.";
			new_line;
			return true;
		],
		react_before [;
			if (noun == d_obj) {
				noun = self;
			}
		],
		reset [;
			self.tried_to_take = false;
		],
	private
		tried_to_take false,
;

Atrezzo	-> -> "huellas"
	has
		female pluralname,
	with
		name_f 'huella',
		name_fp 'huellas',
		name_m 'rastro',
		gender G_FEMENINO + G_PLURAL,
		!!
		description [;
			print "Prácticamente invisibles en la compacta capa de [Cenizas].";
			new_line;
			return true;
		],
;

!! @GrutaPasadizoFuego
Atrezzo -> GrutaPasadizoFuego "fuego"
	class
		Fire,
	with
		name_f 'bestia' 'claridad' 'llama' 'luz',
		name_fp 'llamas',
		name_m 'dragon' 'fuego' 'fulgor' 'monstruo' 'resplandor',
		name_mp 'fuegos',
		gender G_MASCULINO,
		adjectives 'lengua' 'lenguas',
		!!
		description [;
			print "Lenguas de fuego mayores que cualquier hombre iluminan el pasadizo desde la [DirGrutaSalon](guarida) del dragón.";
			new_line;
			return true;
		],
;


!!==============================================================================
!!
!!	4)	Enorme salón subterráneo / guarida del dragón
!!
!!------------------------------------------------------------------------------

!! @DirGrutaSalon
AdjacentRoom DirGrutaSalon
	has
		male,
	with
		found_in GrutaPasadizo,
		short_name [;
			print "[](salón:DIRECTION_OBJECT) subterráneo";
			return true;
		],
		name_f 'guarida' 'madriguera',
		name_m 'cubil' 'nido' 'salon',
		gender G_MASCULINO,
		adjectives 'bestia' 'dragon' 'monstruo' 'subterraneo',
		!!
		adjacent_room GrutaSalon,
		before [;
			Enter:
				if (self.is_hesitating) {
					self.is_hesitating = false;
					Tutorial.hint_hesitations();
					print "La entrada al cubil parece la entrada misma a los avernos. Existen multitud de leyendas acerca de cómo el monstruo llegó a vivir ahí, pero lo cierto es que nadie lo sabe con certeza. Algunos sabios afirman que el dragón es tan antiguo como la propia Tierra y que ha habitado en las profundidades de la gruta desde el origen de los tiempos.
					^^
					Por un momento dudas, preguntándote si tienes realmente el valor necesario para dar el paso.";
					new_line;
					return true;
				} else {
					!! Comprueba el hacha
					if (IndirectlyContains(player, Percunos)) {
						print "Aferrando el [Percunos](hacha:OTHER_OBJECT) con firmeza";
					} else {
						print "Completamente desarmado";
					}
					!! Comprueba el casco:
					if (IndirectlyContains(player, Yelmo) && Yelmo has worn) {
						print ", con el rostro protegido por el viejo [Yelmo] —confías que te traiga más fortuna que a su anterior propietario—";
					} else {
						print ", con la cabeza alta y el rostro descubierto —tal y como dictan los códigos—";
					}
					print ", apartas los temores a un lado y te dispones a encarar a la bestia.";
					!! Se registran las elecciones del jugador:
					if (IndirectlyContains(player, Percunos)
						&& Yelmo hasnt worn) {
						give LogroCodigoCaballeria on;
					}
				}
				new_line;
				return self.AdjacentRoom::before();
		],
		reset [;
			self.is_hesitating = true;
		],
	private
		is_hesitating true,
;

!! @GrutaSalon
Indoors GrutaSalon
	has
		male,
	with
		short_name [;
			print "Enorme [](salón) subterráneo";
			return true;
		],
		name_f
			'caverna' 'cueva' 'estancia' 'gruta' 'guarida' 'madriguera' 'sala',
		name_m 'cubil' 'nido' 'salon',
		gender G_MASCULINO,
		adjectives 'dragon' 'enorme' 'gran' 'grande' 'subterraneo',
		!!
		b_to [;
			<<Enter DirGrutaPasadizo>>;
		],
		before [;
			Smell:
				if (noun == 0 or self) {
					print "El penetrante olor del azufre llena la [self](cueva).";
				} else {
					print "El olor del azufre lo impregna todo.";
				}
				new_line;
				return true;
		],
		cant_go [;
			if (Baron.is_blind) {
				print "Echas a correr hacia la salida absolutamente a ciegas";
				return GrutaBrechas.fall_in();
			}
		],
		exits [;
			print "La boca [del DirGrutaPasadizo] se abre en una de las paredes de roca del enorme [self](salón).";
			new_line;
			return true;
		],
		initial [;
			if (self hasnt visited) {
				StartDaemon(Dragon);
			}
		],
		description [;
			if (Baron.is_blind) {
				print "En un solo instante el mundo ha perdido toda su complejidad. Ahora sólo hay blanco y dolor. Y, en algún lugar, gobernando sobre ambas cosas: [el Dragon], Dios único y omnipotente.";
			} else if (Baron.is_burning) {
				print "Entre la vorágine de llamas sólo aciertas a distinguir imágenes fragmentadas de lo que te rodea; [GrutaBrechas](fisuras) en la roca, la mancha carmesí [del Dragon], el pequeño punto de oscuridad que señala la boca [del DirGrutaPasadizo].";
			} else {
				print "El pasadizo desemboca en lo que parece una [self](gruta) natural. Es enorme, mucho mayor incluso que la sala del trono del Emperador. Desde infinidad de [GrutaBrechas](grietas) y [GrutaBrechas](fisuras) abiertas en la roca emergen violentas llamaradas en intervalos irregulares. La luz aquí es más brillante que el mediodía y el calor asfixiante.";
			}
			new_line;
			return true;
		],
		out_to [;
			<<Enter DirGrutaPasadizo>>;
		],
;

!! @Dragon
Character -> Dragon "dragón"
	has
		concealed male,
	with
		name_f 'bestia' 'criatura',
		name_m 'dragon' 'engendro' 'monstruo',
		gender G_MASCULINO,
		adjectives 'carmesi' 'roja' 'rojo',
		article "el",
		!!
		before [;
			Attack:
				if (Baron.is_blind) {
					print "Enloquecido por el dolor, echas a correr";
					if (IndirectlyContains(player, Percunos)) {
						print " lanzando tajos con el hacha";
					} else {
						print " lanzando puñetazos al aire";
					}
					print ", completamente a ciegas";
					return GrutaBrechas.fall_in();
				} else if (Baron.is_burning) {
					print "El monstruo debe morir. Sea cual sea el precio. Por un instante alcanzas a distinguir entre las llamas el ojo de reptil y preparas";
					if (IndirectlyContains(player, Percunos)) {
						print " el hacha dispuesto a descargarlo directamente sobre él";
					} else {
						print " el puño dispuesto a intentar cegarlo";
					}
					print ". Entonces la cola del dragón se agita y te golpea en un costado, enviándote volando por la gruta en un vertiginoso arco que finaliza abruptamente con tu cabeza impactando contra la pared";
					!! el jugador no podría leer esto a menos que tenga el
					!! casco puesto (si no, el Barón hubiese quedado cegado).
					!! Aún así se añade la comprobación para asegurar la
					!! coherencia del texto ante posibles errores o cambios de
					!! lógica:
					if (IndirectlyContains(player, Yelmo) && Yelmo has worn) {
						print ". El casco amortigua en parte el golpe evitando que pierdas el conocimiento. Aún llegas a ver la cabeza grotesca acercándote a ti. Su boca abierta de par en par. Una nueva tempestad de fuego.";
					} else {
						print ". El cráneo se fractura en el acto y todo se vuelve negro.";
					}
					give LogroGuerreroObstinado on;
					!! TODO - fin de la escena 1
					new_line;
					return true;
				} else {
					print "—¡Escucha bien, bestia! —gritas[if:IndirectlyContains(player, Percunos)], alzando el hacha con ambas manos[else], sorprendido de la seguridad de tus propias palabras[fi]. —¡Ha llegado tu hora! —y, por un instante, la estampa es la de un auténtico caballero sacado de un cuento de hadas; el enfrentamiento del héroe contra el mal.
					^^
					El [self](monstruo) te observa impasible mientras cargas contra él. A pesar de sus dientes largos como una vara, te lanzas a la carrera sin pensártelo, espoleado por una rabia suicida. En ese momento";
					give self on; !! detiene el daemon durante el prox. turno
					return self.spits_fire_on_attack();
				}
			DefaultAction:
				if (Baron.is_blind) {
					<<Examine self>>;
				} else if (self has described
					&& IndirectlyContains(player, Percunos)) {
					<<Attack self>>;
				} else {
					<<Examine self>>;
				}
			TalkTo:
				if (Baron.is_burning) {
					if (self.tried_to_talk_to_again) {
						return ConversacionDragon.final_action();
					}
				}
				give self on; !! detiene el daemon durante el prox. turno
				return ChoiceSetManager.start(ConversacionDragon);
		],
		daemon [;
			!! El daemon gestiona el movimiento del dragón, con sus intentos
			!! por incinerar o comerse al Barón:
			if (self has on) {
				give self ~on;
				return true;
			}
			switch (location) {
				GrutaPasadizo:
				GrutaSalon:
					if (self.counter ~= 3 || Baron.is_blind) {
						new_line;
						return self.reacts();
					}
				GrutaVestibulo:
			}
			self.counter++;
			return true;
		],
		description [;
			if (self hasnt described && ~~Baron.is_burning) {
				print "Si bien creías haberte preparado para lo peor, no estabas preparado para esto.
				^^
				Es grande, desde luego —calculas al menos una treintena de varas desde la punta de la cola hasta la testa—, y sus escamas rojas refulgen con el mismo brillo del acero endurecido. Cualquiera pensaría que ningún arma corriente podría atravesarlas. Pero ya te has enfrentado a monstruos colosales antes y";
				if (IndirectlyContains(player, Percunos)) {
					print " el tuyo no es un hacha ordinario";
				} else {
					print " no hay en el mundo criatura invulnerable a un golpe certero";
				}
				print ". Sin embargo… el *fuego*. Ése que se adivina en sus ojos y en su garganta, el baile de llamas tras la blanca hilera de dientes afilados. Es ese incendio lo que imbuye tu alma de un horror insoportable.
				^^
				No se trata de otro engendro escupe-fuego cualquiera, ni tiene nada que ver con un elemental. No, esto no es natural. Es mucho más aterrador, más insondable, más perverso. Sabes muy bien que ese fuego no podrá ser sometido en absoluto, ningún hombre podrá jamás doblegarlo y obligarlo a ceder a su voluntad; pues lo reconoces y cualquier otra persona lo reconocería también. Este monstruo, esta bestia, este fuego abrasador habita en el rincón más profundo del alma de todos nosotros. Siempre lo ha hecho y siempre lo hará. Y no hay modo alguno de derrotarlo.";
			} else {
				give self on;
				return self.reacts();
			}
			new_line;
			return true;
		],
		spits_fire_on_attack [;
			return self.spits_fire(1);
		],
		spits_fire_on_talk_to [;
			give self on; !! detiene el daemon durante el prox. turno
			return self.spits_fire(2);
		],
		spits_fire_on_wait [;
			return self.spits_fire(0);
		],
		tried_to_talk_to_again false,
	private
		counter 0,
		reacts [;
			switch (location) {
				GrutaSalon:
					if (self.counter == 0) {
						self.counter = 1;
						print "La mole de escamas rojas [del self] yace en el suelo en mitad de la cueva, observándote fijamente con su malévolo ojo amarillo.";
						new_line;
						return true;
					} else if (self.counter == 1) {
						self.counter = 2;
						print "[El self] se incorpora con una agilidad sorprendente para su corpulencia, provocando un temblor que sacude toda la [GrutaSalon](cueva) y que a punto está de tirarte al suelo. Sólo gracias a tus rápidos reflejos y a décadas de experiencia llevando armaduras pesadas eres capaz de mantener el equilibrio.";
						new_line;
						return true;
					} else if (self.counter == 2) {
						self.counter = 3;
						return self.spits_fire_on_wait();
					} else if (self.counter >= 3) {
						StopDaemon(self);
						if (Baron.is_blind) {							
							print "No llegas a verlo acercarse a ti ni a sentirlo a tu lado. No ves ni sientes nada más que blanco y dolor. Un dolor blanco mientras sus dientes atraviesan tu armadura con la misma facilidad con la que atravesarían la piel de una manzana. Algo cruje de forma repugnante y un torrente de sangre te inunda la boca.
							^^
							Después incluso el blanco y el dolor desaparecen.";
						} else {
							print "Desorientado por el torbellino de llamas pierdes de vista al monstruo por un momento. Cuando alcanzas a ver de nuevo su borrón rojo lo tienes ya encima y es demasiado tarde para reaccionar; sus dientes atraviesan la armadura con la misma facilidad con la que atravesarían la piel de una manzana. Hay un crujido repugnante y un torrente de sangre te inunda la boca.
							^^
							Después no hay nada más.";
						}
						give LogroYEseRuido on;
						!! TODO - fin escena 1
						new_line;
						return true;
					}
			}
		],
		spits_fire [ context;
			!! Actualiza estado del Barón
			if (parent(Yelmo) ~= player || Yelmo hasnt worn) {
				Baron.is_blind = true;
			}
			Baron.is_burning = true;
			!! Actualiza estado del Dragón
			if (self.counter < 3) {
				self.counter = 3;
			}
			!! Actualiza estado del resto del mundo
			move GrutaSalonFuego to Limbo;
			move GrutaBaronFuego to Baron;
			ConversacionDragon.remove_choice(DragonAmenazar);
			ConversacionDragon.remove_choice(DragonRazonar);
			ConversacionDragon.add_choice(DragonAmenazarOtraVez);
			ConversacionDragon.add_choice(DragonPedirClemencia);
			!! TODO - iniciar daemon del fuego
			if (context == 1 or 2) {
				!! Descripción al atacar/hablar
				print ", veloz y repentino como un relámpago, un surtidor de fuego estalla desde el interior de su boca y te envuelve por completo.";
				if (Baron.is_blind) {
					print " Tu rostro desprotegido se calcina en la llamarada. El dolor y un color blanco perfecto lo ocupan todo.";
				} else {
					print " El yelmo y la armadura te protegen de lo peor de la llamarada, pero tu cuerpo empieza a arder. Nunca antes habías sentido dolor semejante.";
				}
				new_line;
				if (~~Baron.is_blind) {
					new_line;
					GrutaSalon.description();
				}
				return true;
			} else {
				!! Descipción general
				print "Un inmenso chorro de fuego sale escupido desde la boca [del self] y te alcanza antes siquiera de que puedas pensar en apartarte.";
				if (Baron.is_blind) {
					print " Tu rostro desprotegido se calcina en la llamarada. El dolor y un color blanco perfecto lo ocupan todo.";
				} else {
					print " El yelmo y la armadura te protegen de lo peor de la llamarada, pero tu cuerpo empieza a arder. Nunca antes habías sentido dolor semejante.";
				}
				new_line;
				return true;
			}
		],
;

!! @GrutaBrechas
Atrezzo -> GrutaBrechas "brechas"
	has
		container enterable female open ~openable pluralname,
	with
		name_f 'brecha' 'fisura' 'grieta' 'hendedura' 'hendidura',
		name_fp 'brechas' 'fisuras' 'grietas' 'hendeduras' 'hendiduras',
		name_m 'agujero' 'hoyo' 'socavon',
		name_mp 'agujeros' 'hoyos' 'socavones',
		gender G_FEMENINO + G_PLURAL,
		!!
		before [;
			Enter:
				if (Baron.is_blind) {
					print "Echas a correr presa del pánico hacia donde crees que estaba la grieta más próxima";
					return self.fall_in();
				} else if (Baron.is_burning) {
					print "Echas a correr presa del pánico intentando huir del dragón, hacia las grandes grietas que atraviesan la gruta";
					return self.fall_in();
				}
				print "Ahí no aguarda más que la muerte.";
				new_line;
				return true;
			JumpOver:
				<<Enter self>>;
		],
		description [;
			if (Baron.is_blind) {
				<<Look>>;
			} else {
				print "Amplias brechas fracturan la roca y escupen al aire chorros de fuego más altos que un hombre. Tienes que tener cuidado con dónde pones los pies si no quieres acabar precipitándote su interior.";
				new_line;
				return true;
			}
		],
		fall_in [;
			print ". Tropiezas en el suelo irregular y caes y te incorporas torpemente para continuar la carrera desquiciada mientras las llamas consumen tu cuerpo. Un pie detrás de otro y luego otro y otro. El siguiente pie no encuentra suelo en el que apoyarse y caes de nuevo. Y en esta ocasión no te golpeas contra las rocas. Simplemente sigues cayendo.
			^^
			El mismo fuego que te consume alumbra la oscuridad de la hendidura por la que te precipitas";
			if (Baron.is_blind) {
				print ", aunque ciego tras el ataque del dragón para ti sólo es un abismo blanco. No ves las paredes cristalizadas y perfectamente verticales que se extienden hasta una profundidad mayor de lo que hubiéses podido imaginar. No ves tampoco cuando el estrecho espacio de la fisura se abre finalmente en una gigantesca bóveda subterránea con un lago de magma en el fondo. Atraviesas el espacio vacío como una estrella fugaz, ciego a la sobrecogedora belleza de este mundo subterráneo. Y acabas impactando salvajemente contra la roca fundida";
			} else {
				new_line;
				print ", como una antorcha humana. Sigues cayendo e iluminando las paredes cristalizadas y perfectamente verticales que se extienden a una profundidad mucho mayor de lo que hubiéses podido imaginar, hasta que de pronto el estrecho espacio de la fisura se abre en una gigantesca bóveda subterránea con un lago de magma en el fondo. Durante el intervalo en que se producen cuatro latidos de corazón quedas fascinado ante la belleza sobrecogedora de este mundo subterráneo. Entonces impactas salvajemente contra la roca fundida";
			}
			print ". Todo tu cuerpo es abrasado y arde y tu alma se transforma en fuego, en llamas, en calor, en luz.";
			!! TODO - fin de la escena 1
			give LogroExcavandoDemasiadoProfundo on;
			new_line;
			return true;
		],
		react_before [;
			VagueGo:
				if (Baron.is_burning) {
					<<Enter self>>;
				}
		],
;

!! @GrutaSalonFuego
Atrezzo -> GrutaSalonFuego "fuego"
	class
		Fire,
	with
		description [;
			SetPronoun('-las', self);
			SetPronoun('ellas', self);
			print "Llamas gigantescas brotan del suelo allá dónde mires.";
			new_line;
			return true;
		],
		reset [;
			move self to GrutaSalon;
		],
;

!! @ConversacionDragon
ChoiceSet ConversacionDragon "(Conversación: dragón)"
	with
		final_action [;
			if (~~Dragon.tried_to_talk_to_again) {
				Dragon.tried_to_talk_to_again = true;
				print "Tu laringe está destrozada por el fuego y es incapaz de articular los sonidos. Tan sólo logras emitir un chillido penoso e ininteligible.";
			} else {
				print "Tu laringe está destrozada y es incapaz de articular sonidos.";
			}
			new_line;
			return true;
		],
		initial_action [;
			if (~~Baron.is_burning) {
				self.add_choice(DragonAmenazar);
				self.add_choice(DragonRazonar);
			}
			return self.inter_action();
		],
		inter_action [;
			if (Baron.is_blind) {
				print "Sabes que está ahí, en algún sitio, aunque no puedas verlo.";
			} else if (Baron.is_burning) {
				print "Está ahí, en algún sitio entre las llamas, aunque no puedas verlo.";
			} else {
				print "Sus ojos de reptil están clavados en ti, inteligentes y malévolos.";
			}
			new_line;
			return true;
		]
;

!! @DragonAmenazar
ChoiceSetItem DragonAmenazar
	with
		entry [
			previous_style;
			previous_style = Utils.set_text_style(TEXT_STYLE_STRESSED);
			print "—Ha llegado tu ";
			PRT__("hora", 0, REMARKABLE_OBJECT);
			print ", bestia.";
			Utils.set_text_style(previous_style);
			return true;
		],
		keys 'hora' 'llegado', 
		!!
		append_choice_set true,
		reaction [;
			print "—¡Escucha bien, [Dragon](bestia)! —gritas, sorprendido de la seguridad de tus propias palabras. —¡Ha llegado tu hora! —y, por un instante, la estampa es la de un auténtico caballero sacado de un cuento de hadas; el enfrentamiento del héroe contra el mal.
			^^
			Un instante después";
			return Dragon.spits_fire_on_talk_to();
		],
		relatives DragonRazonar,
;

!! @DragonRazonar
ChoiceSetItem DragonRazonar
	with
		entry [
			previous_style;
			previous_style = Utils.set_text_style(TEXT_STYLE_STRESSED);
			print "—No deberías estar aquí. Tienes que ";
			PRT__("marcharte", 0, REMARKABLE_OBJECT);
			print ".";
			Utils.set_text_style(previous_style);
			return true;
		],
		keys 'aqui' 'deberias' 'estar' 'marcharte' 'marchate' 'no', 
		!!
		append_choice_set true,
		reaction [;
			print "—¡No deberías estar aquí, [Dragon](bestia)! —gritas, tratando de dar a tus palabras una seguridad que en realidad no sientes. —Tienes… tienes que marcharte. ¡Tienes que a-
			^^
			La última frase queda interrumpida cuando";
			return Dragon.spits_fire_on_talk_to();
		],
		relatives DragonAmenazar,
;

!! @DragonAmenazarOtraVez
ChoiceSetItem DragonAmenazarOtraVez
	with
		entry [
			previous_style;
			previous_style = Utils.set_text_style(TEXT_STYLE_STRESSED);
			print "—¿Eso es todo? ¡Ven aquí! ¡";
			PRT__("Atrévete", 0, REMARKABLE_OBJECT);
			print " a acercarte!";
			Utils.set_text_style(previous_style);
			return true;
		],
		keys 'acercarte' 'acercate' 'aqui' 'atrevete' 'ven', 
		!!
		append_choice_set false,
		persistent true,
		reaction [;
			return ConversacionDragon.final_action(true);
		],
;

!! @DragonPedirClemencia
ChoiceSetItem DragonPedirClemencia
	with
		entry [
			previous_style;
			previous_style = Utils.set_text_style(TEXT_STYLE_STRESSED);
			print "—¡No, para! ¡Ten ";
			PRT__("piedad", 0, REMARKABLE_OBJECT);
			print "! Piedad…";
			Utils.set_text_style(previous_style);
			return true;
		],
		keys 'clemencia' 'no' 'para' 'piedad', 
		!!
		append_choice_set false,
		persistent true,
		reaction [;
			return ConversacionDragon.final_action(true);
		],
;
